/// \file Package management interfaces.
/*
* Nirvana runtime library.
*
* This is a part of the Nirvana project.
*
* Author: Igor Popov
*
* Copyright (c) 2021 Igor Popov.
*
* This program is free software; you can redistribute it and/or modify
* it under the terms of the GNU Lesser General Public License as published by
* the Free Software Foundation; either version 3 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU Lesser General Public
* License along with this library.  If not, see <http://www.gnu.org/licenses/>.
*
* Send comments and/or bug reports to:
*  popov.nirvana@gmail.com
*/
#ifndef NIRVANA_PACKAGES_IDL_
#define NIRVANA_PACKAGES_IDL_

#include "BindError.idl"

module Nirvana {

/// Package management API.
module PM {

/// Exported static object type
enum ObjType
{
	OT_PSEUDO, ///< pseudo interface
	OT_OBJECT, ///< interface
	OT_LOCAL,  ///< local interface
	OT_VALUE   ///< abstract valuetype
};

const unsigned short MODULE_FLAG_SINGLETON = 1;

struct Binding
{
	long module_id;
	string binary_path;
	unsigned short platform;
	unsigned short module_flags;
	ObjType type;
};

struct ObjBinding
{
	string name;
	unsigned short major, minor;
	string itf_id;
};

struct Export
{
	ObjBinding binding;
	ObjType type;
};

struct ModuleInfo
{
	string name;
	unsigned short flags;
};

struct ModuleBindings
{
	ModuleInfo mod;
	sequence <Export> exports;
	sequence <ObjBinding> imports;
};

/// Package database interface.
interface PackageDB
{
	/// @brief Obtain binding information for object.
	/// @param name Object name.
	/// @param platform Computational platform.
	/// @param [out] binding Binding information: which module provides the requested object.
	void get_binding (in string name, in PlatformId platform, out Binding binding)
		raises (BindError::Error);

	exception ModuleNotFound {};

	ModuleInfo get_module_info (in ModuleId id) raises (ModuleNotFound);
	void get_module_bindings (in ModuleId id, out ModuleBindings bindings) raises (ModuleNotFound);

	typedef sequence <ModuleId> Modules;

	Modules get_module_dependencies (in ModuleId id) raises (ModuleNotFound);
	Modules get_dependent_modules (in ModuleId id) raises (ModuleNotFound);
	Modules get_modules (in string filter);
};

interface PacMan;

/// Installed packages.
interface Packages : packageDB
{
	/// Start management transaction.
	/// Returns `Nil` if management transaction is already started by other client.
	PacMan manage ();
};

/// Package management transaction interface.
interface PacMan : PackageDB
{
	void commit () raises (BindError::Error);
	void rollback () raises (BindError::Error);

	PlatformId register_binary (in string bin_path, out ModuleInfo info) raises (BindError::Error);

	exception HasDependent
	{
		Modules dependent;
	};

	unsigned long unregister (in ModuleId mod_id, in boolean recursive) raises (BindError::Error, HasDependent);
};

/// Module identifiers <= 100 are reserved for system.
const ModuleId MAX_SYS_MODULE_ID = 100;

};
};

#endif
